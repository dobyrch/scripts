#!/usr/bin/env zsh
setopt extended_glob
setopt null_glob

save_dir="${HOME}/.config/retroarch/saves"
# Delete all but the ten most recent savestates for each game
n='10'

games=( "${save_dir}/"*.state<-># )
games=( "${(u@)games%%<->}" )

for game in "${games[@]}"; do
	# Sort adds a trailing NUL, so delete the last (empty) element
	states=( "${(0)$(find "${game}"<-># -print0 | sort -zV)}" )
	states[-1]=()

	if (( ${#states} < ${n} )); then
		continue
	fi

	for (( i = 0; i < n; ++i )); do
		from="${states[((i-n))]}"
		to="${game}${i%0}"

		cp "${from}" "${to}"
		cp "${from}.png" "${to}.png"
	done

	for (( i = n+1; i <= ${#states}; ++i )); do
		rm "${states[i]}" "${states[i]}.png"
	done

done
